// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
        static int num;

        /** Initializes the library. */
        function void init() {
            let num=0;
            return;
        }

        /** Returns the absolute value of x. */
        function int abs(int x) {
            if(x<0){
                return (-x);
            }
            else{
                return x;
            }
        }
        
        /** This function returns true if the n-th bit is one and false otherwise. Counts from 0 to 15.  */
        function bool bit(int x, int n){
            var int i;
            let i = 0;
            while(i<n){
                // Shift x to the right n times
                let x = #(x);
                let i = (i+1);
            }
            let x=(x&1);
            if (x=1){
                return true;
            }
            else{
                return false;
            }
        }
        /** This function returns the length of x in bits. */
        function int intLength(int x){
            var int n,i;
            let n = 15;
            let i = 0;
            while(i<(n+1)){
                if(Math.bit(x,(n-i))){
                    // Return the most significant bit.
                    return (n-i+1);
                }
                let i = i+1;
            }
            return (n-i+1);
        }

        /** 
         * Returns the product of x and y. 
         * When a Jack compiler detects the multiplication operator '*' in the 
         * program's code, it handles it by invoking this method. In other words,
         * the Jack expressions x*y and multiply(x,y) return the same value.
         */
        function int multiply(int x, int y) {
            // Pseudocode:
            // 1. let sum = 0
            // 2. let shiftedx = x
            // 3. for i = 0 ... n-1 do
            // 4.   if ((i-th bit of y) == 1)
            // 5.      let sum = sum + shiftedx
            // 6.   let shiftedx = 2*shiftedx
            // 7. return sum

            var int sum,shiftedx,i,n;
            let n = Math.intLength(y);
            let sum = 0;
            let  = x;
            let i = 0;
            while(i<n){
                if(Math.bit(y,i)){
                    let sum = (sum+shiftedx);
                }
                // Shift left 
                let shiftedx = ^(shiftedx); 
                let i = i + 1;
            }
            return sum;
        }

        /** 
         * Returns the integer part of x/y.
         * When a Jack compiler detects the multiplication operator '/' in the 
         * program's code, it handles it by invoking this method. In other words,
         * the Jack expressions x/y and divide(x,y) return the same value.
         */
        function int divide(int x, int y) {
            // This should be implemented without using multiplication.
            // Hint: updating the value of 2*q*y in the "if" statement requires
            // at most a single addition operator at every recursion step.
            // Pseudocode:
            // 1. if (y > x) return 0
            // 2. let q = divide(x, 2*y)
            // 3. if ((x - 2*q*y) < y)
            // 4.   return 2*q
            // 5. else
            // 6.   return 2*q + 1
            var int q,absX,absY,result;
            var boolean isNegative;
            let absX=Math.abs(x);
            let absY=Math.abs(y);

            if (y = 0) {
                do Sys.error(3);  // Division by zero error
            }
            // Check if result need to be nagative:
            if ((x<0 & y>0)| (x>0 & y<0)){
                let isNegative = true;
            }
            else{
                let isNegative = false;
            }

            let num=0;

            let result=Math.divideHelper(absX,absY);

            if (isNegative){
                return (-result);
            }
            else{
                return result;
            }
        }

        /** Returns the integer part of the square root of x. */
        function int sqrt(int x) {
            // This should be implemented without using multiplication or division.
            // Pseudocode (where '**' is the mathematical power function):
            // 1. let y = 0
            // 2. for j = (n/2 - 1) ... 0 do
            // 3.   if ((y + 2**j)**2 <= x) then let y = y + 2**j
            // 4. return y
            var int y,j,n,temp,r;
            // Throw a system error if x is negative:
            if (x < 0) {
                do Sys.error(4);  
            }

            let y = 0;
            let n = Math.intLength(x);
            // Same as n/2: 
            let j=#(n);

            while(j>(-1)){
                let temp=Math.shiftLeftTimes(1,j);
                let r=(y+temp);
                let r=r*r;
                if(((r<x)|(r=x))&(r>0)){
                    let y=(y+temp);
                }
                let j=j-1;
            }
            return y;
        }

        /** Returns the greater number. */
        function int max(int a, int b) {
            if (a>b){
                return a;
            }
            else{
                return b;
            }
        }

        /** Returns the smaller number. */
        function int min(int a, int b) {
            if(a<b){
                return a;
            }
            else{
                return b;
            }
        }

        function int divideHelper(int x,int y){
            var int q;
            // Handle the case where y overflows
            if(y<0){
                return 0;
            }
            // Base case
            if(y>x){
                return 0;
            }

            let q = Math.divideHelper(x,(y+y));
            if ((x-num)<y){
                return (q+q);
            }
            else{
                let num=num+y;
                return(q+q+1);
            }

        }

        // Shifts the value left x times.
        function int shiftLeftTimes(int val,int times){
            var int i;
            let i = 0;
            while(i<times){
                let val = ^(val);
                let i=i+1;
            }
            return val;
        }
}
