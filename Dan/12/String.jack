// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array str;
    field int length,maxLen;
    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {

        if(maxLength<0){
            do Sys.error(14);
        }

        if(maxLength=0) {
        let str = Array.new(1);  // Create minimum size array
        }
        else {
            let str = Array.new(maxLength);
        }

        let maxLen = maxLength;
        let length = 0;

        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do str.dispose();
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {

        if((j>length)|(j<0)){
            do Sys.error(15);
        }

        return str[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {

        if((j>length)|(j<0)){
            do Sys.error(16);
        }

        let str[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if(length=maxLen){
            do Sys.error(17);
        }

        let str[length] = c;
        let length = length + 1;
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if(length=0){
            do Sys.error(18);
        }

        let str[length-1] = 0;
        let length = length - 1;
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        // Pseudocode:
        // 1. let val = 0
        // 2. for (i = 0 .. str.length()) do
        // 3.   let d = integer value of str.charAt(i)
        // 4.   let val = (val*10) + d
        // 5. return val
        var int val, i, d;
        var boolean isNeg;
        
        let val = 0;
        let i = 0;
        let isNeg = false;
        
        if((length > 0) & (charAt(0) = 45)) {  // Check for '-' sign
            let isNeg = true;
            let i = 1;
        }

        while((i<length)&(String.isDigit(charAt(i)))){
            let d = String.asciiToInt(charAt(i));
            let val = (val*10)+d;
            let i=i+1;
        }
        
        if(isNeg){
            return -val;
        }
        else{
            return val;
        }
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        // Pseudocode:
        // 1. let lastDigit = val % 10
        // 2. let c = character representing lastDigit
        // 3. if (val < 10)
        // 4.   return c (as a string)
        // 5. else
        // 6.   return int2String(val / 10).appendChar(c)

        var int lastDigit;
        var char c;
        do resetString();

        // Handle negative values.
        if(val < 0) {
            do appendChar(45);  // ASCII for '-'
            let val = Math.abs(val);
        }
        do int2String(val);
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }

    // converts a asccii code the integer value
    function int asciiToInt(char c){
        return c-48;
    }
    // converts integer value to asccii code
    function int intToAscii(char c){
        return c+48;
    }
    
    // Returns if the character is a digit.
    function bool isDigit(char c){
        if((c<48) | (c>57)){
            return false;
        }
        else{
            return true;
        }
    }

    // Returns the last digit of an integer.
    function int getLastDigit(int val){
        var int num;
        let num = val / 10;
        return val-(num*10);
    }

    // Reset the string:
    method void resetString(){
        var int i;
        let length = 0;
        let i=0;
        while(i<maxLen){
            let str[i] = 0;
            let i = i+1;
        }
        return;
    }
    
    // converts a value to string
    method void int2String(int val){
        var int lastDigit;
        var char c;
        let lastDigit = String.getLastDigit(val);
        let c = String.intToAscii(lastDigit);
        if(val<10){
            do appendChar(c);
        }
        else{
            do int2String(val/10);
            do appendChar(c);
        }
        return;
    }
}
