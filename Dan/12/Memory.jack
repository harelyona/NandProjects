// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {
    static Array memory;
    static int freeList,heapBase,heapLength;

    /** Initializes the class. */
    function void init() {
        // Pseudocode:
        // 1. freeList = heapBase
        // 2. freeList.length = heapLength
        // 3. freeList.next = null
        let memory = 0;
        let heapBase = 2048;
        let heapLength = 16383 - heapBase;
        let freeList=heapBase;
        let memory[freeList] = heapLength;
        let memory[freeList+1] = 0; 

        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return memory[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let memory[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. Assume size > 0. */
    function int alloc(int size) {
        // Pseudocode:
        // 1. Search freeList using best-fit or first-fit heuristics to obtain
        //    a segment with segment.length > size.
        //    If no such segment is found, return -1.
        // 2. block = needed part of the found segment (or all of it, if the 
        //            segment remainder is too small).
        // 3. block[-1] = size + 1 // Remember block size, for de-allocation
        // 4. Update freeList to reflect the allocation
        // 5. Return block
        // The figure MemoryTest/MemoryFreeListExample.png illustrates how
        // the freeList should look like.
        var int curr,prev,block,currSize;
        let curr=freeList;
        let prev=0;

        // I will use first-fit method for simplicity:
        while(~(curr=0)){
            // Check the current segement size:
            let currSize=memory[curr];

            // Check if the segment is big enough
            if(currSize>size){

                // If no elements left in the free segment we want to delete it from the freeList:
                if((currSize-size-1)<3){
                    let block=(curr+1);
                    /* set the previous segment to the next segment.
                     If prev is null we update the freeList variable otherwise we update the previous segment to the next pointer.
                    */
                    if(prev=0){
                        let freeList=memory[curr+1];
                    }
                    else{
                        let memory[prev+1]=memory[curr+1];
                    }
                }
                // We carve a block from the free segment:
                else{
                    let block=(curr+currSize-size);
                    let memory[curr]=(currSize-size-1);
                }
                // Set the size of the allocated block:
                let memory[block-1]=(size+1);
                return block;
            }

            // advance the pointer and save the previous segment:
            let prev=curr;
            let curr=memory[curr+1];
        }
        return -1;

    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        // Pseudocode:
        // 1. segment = o - 1
        // 2. Insert segment into the freeList
        var int segment;

        let segment = o - 1;
        let memory[segment+1] = freeList;
        let freeList = segment;
        
        return; 

    }    

    /** Returns the maximal element contained in the given Array/object. 
     *  Assume inputs are initialized and contain at least one element. */
    function int max(Array o) {
        // Hint: the algorithm you need to implement in Memory.alloc saves the
        // size of the allocated block in the memory cell right before the 
        // start of the block, and this can be used to implement Memory.max.

        var int maxVal,length,i;
        let i=1;
        let length=memory[o-1];
        let maxVal=o[0];
        while(i<length){
            if(o[i]>maxVal){
                let maxVal=o[i];
            }
            let i=i+1;
        }
        return maxVal;
    }
}
