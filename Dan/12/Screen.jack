// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
        static int screenAddress;
        static boolean currentColor;

        /** Initializes the Screen. */
        function void init() {
            let screenAddress = 16384;

            // Set the black color to be the default color.
            let currentColor = true;

            return;
        }

        /** Erases the entire screen. */
        function void clearScreen() {
            var int x,y,n,m;
            var boolean prevColor;
            let prevColor=currentColor;
            let x = 0;
            let n = 256;
            let m = 512;
            let currentColor = false;
            while(y < n) {
                let x = 0;  // Reset y for each row
                while(x < m) {
                    do Screen.drawPixel(x,y);
                    let x = x + 1;
                }
                let y = y + 1;
            }
            let currentColor=prevColor;
            return;
        }

        /** Sets the current color, to be used for all subsequent drawXXX commands.
         *    Black is represented by true, white by false. */
        function void setColor(boolean b) {
            let currentColor = b;
            return;
        }

        /** Draws the (x,y) pixel, using the current color. */
        function void drawPixel(int x, int y) {
            // For this function, you will need to calculate the value x%16.
            // It should be calculated quickly without using multiplication or
            // division, using exactly one basic math/logical operation.
            // In addition, calculating 16384 + y * 32 + x/16 should not use
            // division or multiplication.
            // Pseudocode:
            // 1. Compute the RAM address where the (x,y) pixel is
            //    represented: 16384 + (32*y) + (x/16).
            // 2. Use Memory.peek to get the 16-bit value of this address
            // 3. Use some bitwise operation to set (only) the bit that corresponds
            //    to the pixel to the current color.
            // 4. Use Memory.poke to write the modified 16-bit value to the RAM
            // address.
             
            var int mod,div,num,addr,val,mask;

            // mod = x % 16
            let mod = x&15;

            // div = x/16
            let div = Screen.shiftRightTimes(x,4);

            // num = y*32
            let num = Screen.shiftLeftTimes(y,5);

            let addr = screenAddress + num + div;
            let val = Memory.peek(addr);

            // Set the desired bit to 1;
            let mask=Screen.shiftLeftTimes(1,mod);

            if(currentColor){
                // We set the desired bit as black
                let val = val|mask;
            }
            else{
                // We set the desired bit as white
                let val = val&(~mask);
            }
            do Memory.poke(addr,val);
            return;

        }

        /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
        function void drawLine(int x1, int y1, int x2, int y2) {
            // The case where x1 != x2 and y1 != y2 should be implemented without
            // multiplication or division.
            // Pseudocode:
            // 1. let x = x1, y = y1, a = 0, b = 0, diff = 0
            // 2. Compute dx and dy
            // 3. while ((a <= dx) and (b <= dy))
            // 4.   do drawPixel(x+a, y+b)
            // 5.   if (diff < 0) { let a=a+1, diff=diff+dy }
            // 6.   else          { let b=b+1, diff=diff-dx }
            //
            // The case where y1 == y2 can be implemented efficiently by
            // trying to draw as many pixels as possible in a single assignment,
            // similarly to Fill in project 4.
            var int diff,a,b,x,y,dx,dy,ax,ay;
            let x=x1;
            let y=y1;
            let diff=0;
            let a=0;
            let b=0;
            let dx=Math.abs(x2-x1);
            let dy=Math.abs(y2-y1);

            // Handle the x1=x2 or y1=y2 case
            if(x1=x2){
                do Screen.drawLineY(x1,y1,y2);
                return;
            }
            if(y1=y2){
                do Screen.drawLineX(y1,x1,x2);
                return;
            }

            while(((a<dx)|(a=dx))&((b<dy)|(b=dy))){
                // Handle direction to draw next
                if((x1<x2)&(y1<y2)){
                    do Screen.drawPixel(x+a,y+b);
                }
                if((x2<x1)&(y1<y2)){
                    do Screen.drawPixel(x-a,y+b);
                }
                if((x1<x2)&(y2<y1)){
                    do Screen.drawPixel(x+a,y-b);
                }
                if((x2<x1)&(y2<y1)){
                    do Screen.drawPixel(x-a,y-b);
                }
                if(diff<0){
                    let a=a+1;
                    let diff=diff+dy;
                }
                else{
                    let b=b+1;
                    let diff=diff-dx;
                }
            }
            return;
        }

        /** Draws a filled rectangle whose top left corner is (x1, y1)
         * and bottom right corner is (x2,y2), using the current color. */
        function void drawRectangle(int x1, int y1, int x2, int y2) {
            var int x;
            let x=x1;
            while(x<(x2+1)){
                do Screen.drawLine(x,y1,x,y2);
                let x=x+1;
            }
            return;
        }

        /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
        function void drawCircle(int x, int y, int r) {
            // This can be implemented efficiently by drawing multiple lines at
            // each iteration. You can use multiplication and sqrt.
            // Pseudocode:
            // 1. for (dy = -r ... r)
            // 2.   let halfWidth = sqrt(r*r - dy*dy)
            // 3.   do drawLine(x-halfWidth, y+dy, x+halfWidth, y+dy)

            var int dx,dy,halfWidth;
            if(r>181){
                let r=181;
            }
            let dy=0;
            while(dy<(r+1)){
                let halfWidth=Math.sqrt((r*r)-(dy*dy));
                do Screen.drawLine(x-halfWidth,y+dy,x+halfWidth,y+dy);
                do Screen.drawLine(x-halfWidth,y-dy,x+halfWidth,y-dy);
                let dy=dy+1;
            }
            return;
        }

        // Shifts the value right x times.
        function int shiftRightTimes(int val,int times){
            var int i;
            let i = 0;
            while(i<times){
                let val = #(val);
                let i=i+1;
            }
            return val;
        }

        // Shifts the value left x times.
        function int shiftLeftTimes(int val,int times){
            var int i;
            let i = 0;
            while(i<times){
                let val = ^(val);
                let i=i+1;
            }
            return val;
        }

        // Draw a straight line from y1 to y2 in x row
        function void drawLineY(int x,int y1,int y2){
            if(y1<y2){
                while(y1<(y2+1)){
                    do Screen.drawPixel(x,y1);
                    let y1=y1+1;
                }
            }
            else{
                while(y2<(y1+1)){
                    do Screen.drawPixel(x,y2);
                    let y2=y2+1;
                }
            }
            return;
        }
        // Draw a straight line from x1 to x2 in y column
        function void drawLineX(int y,int x1,int x2){
            if(x1<x2){
                while(x1<(x2+1)){
                    do Screen.drawPixel(x1,y);
                    let x1=x1+1;
                }
            }
            else{
                while(x2<(x1+1)){
                    do Screen.drawPixel(x2,y);
                    let x2=x2+1;
                }
            }
            return;
        }
}
