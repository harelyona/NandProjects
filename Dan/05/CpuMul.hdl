// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

// This chip is an extension of the regular CPU that uses the extended ALU.
// If instruction[15]==0 or (instruction[14]==1 and instruction[13]==1),
// then CpuMul behaves exactly the same as the regular CPU.
// If instruction[15]==1 and instruction[14]==0 the chip will behave as follows:
// | Instruction           | 15 | 14 | 13 | a | c1 | c2 | c3 | c4 | c5 | c6 |
// |-----------------------|:--:|:--:|:--:|:-:|:--:|:--:|:--:|:--:|:--:|:--:|
// | Regular a-instruction |  0 |  * |  * | * |  * |  * |  * |  * |  * |  * |
// | Regular c-instruction |  1 |  1 |  1 | * |  * |  * |  * |  * |  * |  * |
// | dest=A<<;jump         |  1 |  0 |  1 | 0 |  1 |  0 |  0 |  0 |  0 |  0 |
// | dest=D<<;jump         |  1 |  0 |  1 | 0 |  1 |  1 |  0 |  0 |  0 |  0 |
// | dest=M<<;jump         |  1 |  0 |  1 | 1 |  1 |  0 |  0 |  0 |  0 |  0 |
// | dest=A>>;jump         |  1 |  0 |  1 | 0 |  0 |  0 |  0 |  0 |  0 |  0 |
// | dest=D>>;jump         |  1 |  0 |  1 | 0 |  0 |  1 |  0 |  0 |  0 |  0 |
// | dest=M>>;jump         |  1 |  0 |  1 | 1 |  0 |  0 |  0 |  0 |  0 |  0 |
// Where:
// - "<<" is a left shift, and ">>" is a right shift, as defined in project 2.
//   These notations were chosen because they are used in real programming
//   languages.
// - dest and jump can take the same values as in the regular CPU.

CHIP CpuMul {
    IN  
        inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).
    OUT 
        outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction
    
    PARTS:
    // Choose between instruction input from memory or ALU output:
	Mux16(a= instruction, b= outALU, sel= instruction[15], out= inARegister);
    // Check if the instruction is an C instruction and the dest is an A Register:
    And(a= instruction[15], b= instruction[5], out= isCandAdest);
    // Check if the instruction is an A instruction:
    Not(in=instruction[15] , out= isAinstruction);
    // If A is dest or if A instruction we want to load the A register with a new value:
    Xor(a = isCandAdest, b = isAinstruction, out = loadARegister);
    // A Register:
    ARegister(in=inARegister , load= loadARegister , out= outARegister, out[0..14] = addressM, out = inPC);

    // D Register:
    // Load D register if C instruction and D destination.
    And(a= instruction[4], b= instruction[15], out= loadDRegister);
    DRegister(in= outALU, load= loadDRegister, out= outDRegister);

    // Choose between M or A input (M is from memory and A is from A register):
    Mux16(a= outARegister, b= inM , sel= instruction[12], out= out2Mux);

    // Extended ALU:
    ExtendAlu(x= outDRegister, y= out2Mux, instruction=instruction[6..14], out= outALU, out= outM, zr= isZero, ng= isNegative);

    // Check if zero jump conditions are met:
    And(a= instruction[1], b= isZero, out= zeroJMP);
    // Check if negative jump conditions are met:
    And(a= instruction[2], b= isNegative, out= negJMP);
    // Check if positive jump conditions are met:
    Not(in= isNegative, out= isNotNegative);
    Not(in= isZero, out= isNotZero);
    And(a= isNotNegative, b= isNotZero, out= isPositive);
    And(a= instruction[0], b= isPositive, out= posJMP);
    // If one of the conditions is met we want to load the pc with the data in A Register:
    Or8Way(in[0]= zeroJMP,in[1]= negJMP, in[2]= posJMP,in[3..7]=false, out= toJMP);
    // Check if C instruction:
    And(a= instruction[15], b= toJMP, out= toJMPandC);
    // If None of jmp conditions are met we want to increment:
    Not(in= toJMPandC, out= toInc);
    // PC and jump functionality:
    PC(in= inPC, load= toJMPandC, inc= toInc, reset= reset, out[0..14]= pc);

    // If the 4th bit is 1 in a C instruction we want to write to M:
    And(a= instruction[3], b= instruction[15], out= writeM);
}
