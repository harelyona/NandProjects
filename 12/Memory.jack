// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {
    /** Initializes the class. */
    static int freeList, heapBase;
    static Array RAM;

    function void init() {
        // Pseudocode:
        // 1. freeList = heapBase
        // 2. freeList.length = heapLength
        // 3. freeList.next = null
        let RAM = 0;
        let heapBase = 2048;
        let freeList = heapBase;
        let RAM[freeList] = 16383 - heapBase;
        let RAM[freeList + 1] = 0;
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return RAM[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
    let RAM[address] = value;
    return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. Assume size > 0. */
    function int alloc(int size) {
        var int prv, cur, next, curSize, remainder, allocatedBlockSize;
        if (size < 0) {
            do Sys.error(5);
        }
        let cur = freeList;
        let prv = 0;
        let allocatedBlockSize = size + 2;
        while (cur > 0) { // (cur=0 means end of list)
            let curSize = Memory.peek(cur);
            if (curSize > allocatedBlockSize - 1) { //curSize >= allocatedBlockSize
                let next = Memory.peek(cur + 1);
                if (curSize - allocatedBlockSize > 1) { //curSize - allocatedBlockSize >= 2
                   let remainder = cur + allocatedBlockSize;
                   do Memory.poke(remainder, curSize - allocatedBlockSize);
                   do Memory.poke(remainder + 1, next);
                   if (prv = 0) {
                       let freeList = remainder;
                   } else {
                       do Memory.poke(prv + 1, remainder);
                   }
                } else {
                    if (prv = 0) {
                        let freeList = next;
                    } else {
                        do Memory.poke(prv + 1, next);
                    }
                    let allocatedBlockSize = curSize;
                }
                do Memory.poke(cur, allocatedBlockSize);
                return cur + 2;
            }
            let prv = cur;
            let cur = Memory.peek(cur + 1);
        }
        return -1;
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        // Pseudocode:
        // 1. segment = o - 1
        // 2. Insert segment into the freeList
        do Memory.poke(o - 1, freeList);
        let freeList = o - 2;
        return;
    }

    /** Returns the maximal element contained in the given Array/object.
     *  Assume inputs are initialized and contain at least one element. */
    function int max(Array o) {
    var int max_val, i, val, length, size, end;
    let size = Memory.peek(o - 2);
    let length = size - 2;
    let max_val = Memory.peek(o);
    let i = o + 1;
    while (i < o + length) {
        let val = Memory.peek(i);
        if (val > max_val) {
            let max_val = val;
        }
        let i = i + 1;
    }
    return max_val;
    }
}
