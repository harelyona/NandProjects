// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
        static bool color;
        static int screen;
        /** Initializes the Screen. */
        function void init() {
        let screen = 16384;
        let color = true;
        return;
        }

        /** Erases the entire screen. */
        function void clearScreen() {
            var int i;
            let i = screen;
            while (i < (24575 + 1)) {
                do Memory.poke(i,0);
                let i = i + 1;
            }
            return;
        }

        /** Sets the current color, to be used for all subsequent drawXXX commands.
         *    Black is represented by true, white by false. */
        function void setColor(boolean b) {
            let color = b;
            return;
        }

        /** Draws the (x,y) pixel, using the current color. */
        function void drawPixel(int x, int y) {
            // For this function, you will need to calculate the value x%16.
            // It should be calculated quickly without using multiplication or
            // division, using exactly one basic math/logical operation.
            // In addition, calculating 16384 + y * 32 + x/16 should not use
            // division or multiplication.
            // Pseudocode:
            // 1. Compute the RAM address where the (x,y) pixel is
            //    represented: 16384 + (32*y) + (x/16).
            // 2. Use Memory.peek to get the 16-bit value of this address
            var int addr,ramVal,bitPos;
            let addr = Screen.getRamAddress(x,y);
            let ramVal = Memory.peek(addr);
            // 3. Use some bitwise operation to set (only) the bit that corresponds
            //    to the pixel to the current color.
            let bitPos = Screen.getBitPos(x);
            if(color){
                let ramVal = ramVal | bitPos;
            }
            else{
                let ramVal = ramVal & (~bitPos);
            }
            // 4. Use Memory.poke to write the modified 16-bit value to the RAM
            // address.
            do Memory.poke(addr,ramVal);
            return;
        }

        /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
        function void drawLine(int x1, int y1, int x2, int y2) {
            var int x, y, a, b, diff, dx, dy, stepX, stepY;

            if (x1 = x2) {
                do Screen.drawVerticalLine(x1, y1, y2);
                return;
            }
            if (y1 = y2) {
                do Screen.drawHorizontalLine(x1, y1, x2);
                return;
            }

            let dx = Math.abs(x2 - x1);
            let dy = Math.abs(y2 - y1);
            let x = x1;
            let y = y1;
            let a = 0;
            let b = 0;
            let diff = 0;
            if (x1 < x2) {
                let stepX = 1;
            } else {
                let stepX = -1;
            }
            if (y1 < y2) {
                let stepY = 1;
            } else {
                let stepY = -1;
            }
            while ((a < dx + 1) & (b < dy + 1)) {
                do Screen.drawPixel(x, y);
                if (diff < 0) {
                    let a = a + 1;
                    let diff = diff + dy;
                    let x = x + stepX;
                }
                else {
                    let b = b + 1;
                    let diff = diff - dx;
                    let y = y + stepY;
                }
            }
            return;
        }

        /** Draws a filled rectangle whose top left corner is (x1, y1)
         * and bottom right corner is (x2,y2), using the current color. */
        function void drawRectangle(int x1, int y1, int x2, int y2) {
            var int y, yMin, yMax;
            let yMin = Math.min(y1, y2);
            let yMax = Math.max(y1, y2);
            let y = yMin;
            while (y < (yMax + 1)) {
                do Screen.drawHorizontalLine(x1, y, x2);
                let y = y + 1;
            }
            return;
        }

        /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
        function void drawCircle(int x, int y, int r) {
            // This can be implemented efficiently by drawing multiple lines at
            // each iteration. You can use multiplication and sqrt.
            // Pseudocode:
            // 1. for (dy = -r ... r)
            var int dy, halfWidth;
            let dy = -r;
            while (dy < (r + 1)) {
                // 2.   let halfWidth = sqrt(r*r - dy*dy)
                // 3.   do drawLine(x-halfWidth, y+dy, x+halfWidth, y+dy)
                let halfWidth = Math.sqrt((r * r) - (dy * dy));
                do Screen.drawLine(x - halfWidth, y + dy, x + halfWidth, y + dy);
                let dy = dy + 1;
            }
            return;
        }

        function int getRamAddress(int x, int y) {
            var int shiftedy,div;
            let shiftedy = Screen.shiftLeft(y,5); // x*32
            let div = Screen.shiftRight(x,4); // x/16
            return screen + shiftedy + div;
        }

        function int shiftLeft(int val,int times){
            var int i;
            let i = 0;
            while(i<times){
                let val = ^(val);
                let i=i+1;
            }
            return val;
        }
        function int shiftRight(int val,int times){
            var int i;
            let i = 0;
            while(i<times){
                let val = #(val);
                let i=i+1;
            }
            return val;
        }
        function int getBitPos(int x){
        var int mod,bitPos;
        let mod = x & 15;
        let bitPos = Screen.shiftLeft(1,mod);
        return bitPos ;
        }

        function void drawHorizontalLine(int x1, int y1, int x2){
            var int diff, temp;
            let diff = 0;
            if (x1 > x2) {
                let temp = x1;
                let x1 = x2;
                let x2 = temp;
            }
            while ((x1 + diff) < (x2 + 1)){
                do Screen.drawPixel(x1 + diff,y1);
                let diff = diff + 1;
            }
            return;
        }
        function void drawVerticalLine(int x1, int y1, int y2){
            var int diff, temp;
            let diff = 0;
            if (y1 > y2) {
                let temp = y1;
                let y1 = y2;
                let y2 = temp;
            }
            while ((y1 + diff) < (y2 + 1)){
                do Screen.drawPixel(x1,y1 + diff);
                let diff = diff + 1;
            }
            return;
        }
}
